import groovy.json.JsonOutput

group = 'com.powple'
version = '1.0-SNAPSHOT'
description = """Example of how to configure a simple IDE environment with code completion for developing NXRM3 
Integrations scripts"""

apply plugin: 'groovy'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

ext.versions = [
    nxrm3: '3.15.2-01'
]

if (!(project.hasProperty('ci') && project.property('ci') == true)) {
    println "In Dev mode"
    dependencies {
        compile group: 'org.sonatype.nexus', name: 'nexus-core', version: versions.nxrm3
        compile group: 'org.sonatype.nexus', name: 'nexus-script', version: versions.nxrm3
        compile group: 'org.sonatype.nexus', name: 'nexus-security', version: versions.nxrm3
        compile group: 'org.sonatype.nexus', name: 'nexus-repository', version: versions.nxrm3
        compile group: 'org.sonatype.nexus.plugins', name: 'nexus-repository-maven', version: versions.nxrm3
        compile group: 'org.sonatype.nexus.plugins', name: 'nexus-script-plugin', version: versions.nxrm3
    }
}

task transformScriptToJson(type: Copy) {
    from("src/main/scripts") {
        include('**/*.groovy')
        rename(/(.*)\.groovy/, '$1.json')
        eachFile {
            //first exclude this file for further copy otherwise it will replace our modification
            it.exclude()
            //apply modification: groovy script in json nexus API format
            def script = [
                name: name.replace('.json', ''),
                type: 'groovy',
                content: it.file.text
            ]
            def outputFile = new File(destinationDir.path, it.path)
            boolean copied = it.copyTo(outputFile)
            if (copied) {
                outputFile.text = JsonOutput.toJson(script)
            }
        }
    }
    from("src/main/scripts") {
        include('**/*.sh')
    }
    into("$buildDir/scripts")
}

